package sbnz;

import sbnz.model.User;
import sbnz.model.MealHistory;
import sbnz.model.Ingredient;
import sbnz.model.IngredientQuantity;
import java.time.LocalDate;
import java.util.List;
import java.util.ArrayList;

rule "Calculate entered daily calories"
	agenda-group "dailyCalories"
	no-loop
	when 
		$date: LocalDate()
		$user: User()
		$mealHistory: MealHistory($meals: meals, $date == date, $user == user)
		accumulate(
		 	IngredientQuantity($quantity: quantity, $ingredient: ingredient) from $meals
		 	and Ingredient($calories: calories) from $ingredient,
		 	$total: sum($calories * $quantity)
		 )
	then
		modify ( $user ) { setCaloriesConsumed($total); }		
		System.out.println("Konzumirano " + $total);
		
end

rule "Find meal history for specific user on specific day"
	agenda-group "dailyCalories"
	no-loop
	lock-on-active
	when
		$user: User( $mealHistories : mealHistories )
		$date: LocalDate()
		$dailyMealHistory : MealHistory();
	    $foundMealHistories : List () from collect( MealHistory( meals != null, $date == date, $user == user ) from $mealHistories)
	then
		if ($foundMealHistories.size() > 0) {
			for (Object mealHistory : $foundMealHistories){
				$dailyMealHistory.setId( ((MealHistory) mealHistory).getId() );
				$dailyMealHistory.setMeals (((MealHistory) mealHistory).getMeals() );
				$dailyMealHistory.setDate( ((MealHistory) mealHistory).getDate() );
				$dailyMealHistory.setUser( ((MealHistory) mealHistory).getUser() );
				update ( $dailyMealHistory );
			}
		} else {
			$dailyMealHistory.setDate($date);
			$dailyMealHistory.setUser($user);
			$dailyMealHistory.setMeals(new ArrayList<IngredientQuantity>());
			update ( $dailyMealHistory );
			System.out.println("Create new one ");
		}
end

rule "Insert daily meal into users meal history for that day"
	agenda-group "dailyCalories"
	no-loop
	lock-on-active
	when
		$dailyMealHistory: MealHistory( $meals : meals )
		$dailyMeals : List()
	then
		$meals.addAll($dailyMeals);
		update ( $dailyMealHistory );
		System.out.println("Add meals " + $meals.size());
		
end

rule "Warning: daily intake of calories is passed"
	agenda-group "dailyCalories"
	when 
		$user: User( caloriesConsumed != null, recommendedDailyCalories!= null, caloriesConsumed > recommendedDailyCalories, $excess: (caloriesConsumed - recommendedDailyCalories))
	then
		modify ( $user ) { setWarningForCalories(String.format("You passed your daily intake of calories for %s calories.", $excess)); }
end


rule "Left number of calories to take today"
	agenda-group "dailyCalories"
	when 
		$user: User( caloriesConsumed != null, recommendedDailyCalories!= null, caloriesConsumed < recommendedDailyCalories, $leftCalories: (recommendedDailyCalories - caloriesConsumed))
	then
		modify ( $user ) { setWarningForCalories(String.format("It is left %s calories to take today", $leftCalories)); }
end